locals {
  common_tags = {
    Project     = var.project_name
    Environment = var.environment
    Owner       = var.owner
    ManagedBy   = "Terraform"
  }

  backend_bucket_name = "${var.project_name}-${var.environment}-tfstate"
  dynamodb_table_name = "${var.project_name}-${var.environment}-terraform-locks"
}

resource "aws_s3_bucket" "terraform_state" {
  bucket = local.backend_bucket_name

  tags = merge(
    local.common_tags,
    {
      Name = local.backend_bucket_name
    }
  )
}

resource "aws_s3_bucket_versioning" "terraform_state" {
  bucket = aws_s3_bucket.terraform_state.id

  versioning_configuration {
    status = "Enabled"
  }
}

resource "aws_s3_bucket_server_side_encryption_configuration" "terraform_state" {
  bucket = aws_s3_bucket.terraform_state.id

  rule {
    apply_server_side_encryption_by_default {
      sse_algorithm = "AES256"
    }
  }
}
